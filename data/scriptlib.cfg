//include (extened exec)
//include [module] [cmd]
include = [
	_argi = $numargs
	if ( ( ifreg (concatword (scriptpath) $arg1 "/__init__.cfg") ) ) [
		exec (concatword (scriptpath) $arg1 "/__init__.cfg")
		
		if ( > (matchlist (getgame) $__mods__) -1) [
			if ( = $_argi 1 ) [
				__init__
				looplist i $__modules__ [
					exec (concatword (	scriptpath) $arg1 "/" $i ".cfg")
					alias (concatword $arg1 "_" $i) $__alias__
					__init__
					__alias__ = []
					__init__ = []
					__modules__ = []
				]
			] [
				if (= $_argi 2) [
					if (> (matchlist $arg2 $__modules__) -1) [
						exec (concatword (scriptpath) $arg1 "/" $arg2 ".cfg")
						alias (concatword $arg1 "_" $arg2) $__alias__
						__init__
						__alias__ = []
						__init__ = []
					]
				]
			]
		] [
			echo module $arg1 does not work in this game
		]
	] [
		echo module $arg1 not found
	]
	
	__mods__ = []
	__modules__ = []
	__init__ = []
]


//dictionary lookup for include and help
dict = [
	_ret = []

	if ( = $numargs 0 ) [
		loopfiles i (scriptpath) "" [
			if ( && (ifdir (concatword (scriptpath) $i)) (&& (! (strcmp ".." $i)) (! (strcmp "." $i)) ) ) [ 
				_ret = ( concat $_ret $i ) 
			] 
		] 
	] [
		if (&& ( ifreg (concatword (scriptpath) $arg1 "/__doc__.cfg"))  ( ifreg (concatword (scriptpath) $arg1 "/__init__.cfg") ) ) [
			exec (concatword (scriptpath) $arg1 "/__init__.cfg")
			exec (concatword (scriptpath) $arg1 "/__doc__.cfg")

		
			if ( = $numargs 1 ) [
				_ret = $__modules__
			] [
				if ( = $numargs 2 ) [
					_ret = (matchlist $arg2 $__modules__)
					if ( > $_ret  -1) [ _ret = (at $__doc__ $_ret)] [ _ret = [] ]
				]
			]
		]
	]
	__mods__ = []
	__modules__ = []
	__init__ = []
	__doc__ = []

	result $_ret
]

//help [module] [cmd]
help = [ 
	_ret = []
	if ( = $numargs 0) [ echo  help: (dict) ] [
		if ( = $numargs 1 ) [
			if ( ( strlen (dict $arg1) ) ) [ 
				echo commands in $arg1 : (dict $arg1)
			] [
				echo no help found on that topic
			]
		] [
			if ( = $numargs 2) [
				if ( ( strlen (dict $arg1 $arg2) ) ) [ 
					echo (concatword $arg1 "_" $arg2) - (dict $arg1 $arg2)
				] [
					echo no help found on that topic
				]
			]
		]
	]
]

